actor Grarg replaces DoomImp
{
	Health 120
	Radius 16
	Height 56
	Speed 14
	PainChance 240
	Mass 100
	Scale 0.7
	Bloodcolor "e8 00 21"
	Monster
	Dropitem "Ammo357",64
	SeeSound "Grarg/Sight"
	DeathSound "Grarg/Death"
	ActiveSound "Grarg/Barks"
	PainSound "Grarg/Pain"
	Obituary "Amerigo was ripped to pieces by a Grarg."
	+FLOORCLIP
	+SEEFRIENDLYMONSTERS

	States
	{
	Spawn:
		TNT1 A 0 A_Jump(20, "Grumble")
		GRRG A 4 A_Look()
		Loop

	See:
		GRRG BBCCDDEE 2 A_Chase
		Loop

	Melee:
		TNT1 A 0 A_PlaySound("Grarg/Bite")
		GRRG FG 6 A_FaceTarget
		GRRG H 6 A_CustomMeleeAttack(random(3, 5) * 5, "imp/melee", "Melee", true)
		Goto See

	Missile:
		TNT1 A 0 A_PlaySound("Grarg/Bite")
		GRRG A 6 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0, 40, 0, 0)
		TNT1 A 0 ThrustThing(angle * 256 / 360, 20, 0, 0)
	Jumpup:
		GRRG G 5
		GRRG H 2 A_CustomMeleeAttack(random(2, 4) * 5, "imp/melee", "Melee", true)
	JumpDown:
		GRRG H 2 A_CheckFloor("Land")
		GRRG H 2
		GRRG H 3 A_CustomMeleeAttack(random(2, 4) * 5, "imp/melee", "Melee", true)
		Loop

	Land:
		GRRG A 8 A_Stop
		Goto See

	Grumble:
		TNT1 A 0 A_PlaySound("Grarg/Idle",CHAN_AUTO,0.25)
		Goto Spawn

	Pain:
		GRRG I 2 A_Pain
		Goto See

	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_PlaySound("Grarg/Death")
		GRGD AB 4
		GRGD CD 8
		GRGD EFGH 4
		TNT1 A 0 A_NoBlocking

		GRGD I -1
		Stop
	}
}

actor GLG replaces ZombieMan
{
	Health 50
	Radius 16
	Height 48
	Speed 10
	PainChance 255
	Mass 100
	Scale 0.7
	Monster
	Bloodcolor "e8 00 21"	
	SeeSound "GLG/Attack"
	DeathSound "GLG/Death"
	ActiveSound "GLG/Idle"
	PainSound "GLG/Pain"
	Obituary "Amerigo was slashed to ribbons by a Lesco."
	+FLOORCLIP
	+SEEFRIENDLYMONSTERS
	+JUMPDOWN

	States
	{
	Spawn:
		TNT1 A 0 A_Jump(20, "Grumble")
		GGLG A 4 A_Look()
		Loop

	JumpDown:
		GGLG C 1 A_CheckFloor("Land")
		GGLG C 1
		GGLG C 1 A_CustomMeleeAttack(random(3, 5) * 5, "imp/melee", "Melee", true)
		Loop

	Land:
		GGLG FA 2 A_Stop
		Goto See

	See:
		GGLG AABBCCDD 2 A_Chase
		Loop

	Missile:
		GGLG EF 6 A_FaceTarget
		TNT1 A 0 A_SpawnProjectile("RockProjectile", 56)
		TNT1 A 0 A_PlaySound("GLG/Attack")
		GGLG G 6
		Goto See

	Melee:
		TNT1 A 0 A_PlaySound("GLG/Attack")
		GGLG EF 6 A_FaceTarget
		GGLG G 6 A_CustomMeleeAttack(random(2, 5) * 3, "imp/melee", "Melee", true)
		Goto See

	Grumble:
		TNT1 A 0 A_PlaySound("GLG/Idle",CHAN_AUTO,0.25)
		Goto Spawn

	Pain:
		GGLG H 2 A_Pain
		Goto See

	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_PlaySound("GLG/Death")
		GGLD A 7
		GGLD BCD 4
		TNT1 A 0 A_NoBlocking
		GGLD E -1
		Stop
	}
}

actor RockProjectile
{
	Alpha 1
	Radius 8
	Height 8
	MissileHeight 8
	Speed 30
	Gravity 0.1
	Scale 0.7
	Damage (5)
	Projectile
	Seesound "rock/bounce"
	DamageType "Normal"
	+BLOODSPLATTER
	+DOOMBOUNCE
	-NOGRAVITY

	States
	{
	Spawn:
		PROC ABCDEGHABCDEGH 3
		TNT1 A 0 A_SetGravity(0.5)
		Loop

	XDeath:
		TNT1 A 0 A_PlaySound("rock/ow")
		Stop

	Death:
		PROC A 12
		Stop
	}
}

actor Wyvern replaces Cacodemon
{

	Health 350
	Mass 150
	Speed 14
	Height 56
	Radius 32
	Painchance 220
	Bloodcolor "e8 00 21"	
	Monster
	Obituary "Amerigo met a Wyvern."
	Seesound "Wyvern/pain"
	Deathsound "Wyvern/death"
	painsound "Wyvern/pain"
	+FLOORCLIP
	+FLOAT	
	+NOGRAVITY
	States
	{
	Spawn:
		WYVN ABC 5 A_Look
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 5 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIf (health <= 100, "Rage")	
		TNT1 A 0 A_Setspeed(14)	
		WYVN ABC 3 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 3 A_Chase
		Loop
		
	Rage:	
		TNT1 A 0 A_Setspeed(20)
		WYVN ABC 2 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 2 A_Chase
		WYVN ABC 2 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 2 A_Chase	
		WYVN ABC 2 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 2 A_Chase
		goto see
		
	Missile:
		WYVN F 5 A_FaceTarget
		WYVN E 5 A_FaceTarget
		WYVN A 5 BRIGHT A_HeadAttack
		Goto See
	Pain:
		WYVN F 3
		WYVN F 3 A_Pain
		WYVN F 6
		Goto See
	Death:
		WYVD A 8 A_Playsound("Wyvern/scream",CHAN_AUTO)
		WYVD B 8 A_Scream
		WYVD C 8
		WYVD D 8
		WYVD D 8 
		WYVD D 4 A_JumpIf (z <= floorz, "Crash")
		Wait
	Crash:
		TNT1 A 0 A_Playsound("Wyvern/fall",CHAN_AUTO)	
		WYVD DE 5 A_NoBlocking
		WYVD F -1
		Stop
	
	
	
	Raise:
		WYVD F 8 A_UnSetFloorClip
		WYVD EDCBA 8
		Goto See
	}

}

actor Shark replaces PainElemental
{
	Health 100
	Mass 100
	Speed 10
	Radius 30
	Height 30
	Monster
	Obituary "Amerigo was eaten by a shark."
	SeeSound "shark/sight"
	DeathSound "shark/death"
	ActiveSound "shark/active"
	+FLOORCLIP
	+NOGRAVITY
	+FLOAT

	States
	{
	Spawn:
		SHRK BC 10 A_Look
		Loop

	See:
		SHRK ABCDEFGH 4 A_GiveInventory("Chase_Water", 1)
		Loop

	Melee:
		SHRK E 10 A_FaceTarget
		SHRK F 8 A_CustomMeleeAttack (random(1, 5) * 5, "Chomp", "Swipe")
		SHRK G 10
		Goto See

	Death:
		SHRK A 0
		SHRK A 0 A_Scream
		SHRK AAAAAAAA 0 A_CustomMissile("Gibs1", random(20, 40), 0, random(0, 360), 2, random(-100, 100))
		Stop
	}
}

actor Chase_Water : CustomInventory
{
	Inventory.MaxAmount 0
	+INVENTORY.AUTOACTIVATE

	States
	{
	Spawn:
		TNT1 A 1
		Fail

	Use:
		TNT1 A 0 A_JumpIf(waterlevel < 3, "WaterLimit")
		TNT1 A 0 A_Chase
		Stop

	WaterLimit:
		TNT1 A 0 ThrustThingZ(0, 10, 1, 1)
		TNT1 A 0 A_Chase
		Stop

	}
}

// From IOP
actor Gibs1
{
	Radius 10
	Height 10
	Mass 6
	Speed 14
	+DOOMBOUNCE
	+CLIENTSIDEONLY
	-NOCLIP

	States
	{
	Spawn:
		TNT1 AA 0 A_Jump(225, "Spawn2", "Spawn3", "Spawn4", "Spawn5", "Spawn6", "Spawn7", "Spawn8", "Spawn9", "Spawn10", "Spawn11", "Spawn12", "Spawn13")
		GIB1 AAAAAAA 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB1 A 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn2:
		TNT1 AA 0 A_Jump(200, "Spawn9", "Spawn10", "Spawn11", "Spawn12", "Spawn13")
		GIB1 BBBBBBB 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB1 B 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn3:
		GIB2 AAAAAAA 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB2 A 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn4:
		GIB2 BBBBBBB 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB2 B 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn5:
		GIB2 CCCCCCC 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB2 C 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn6:
		GIB3 BBBBBBB 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB3 B 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn7:
		GIB3 CCCCCCC 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB3 C 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn8:
		GIB4 AAAAAAA 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB4 A 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn9:
		GIB4 AAAAAAA 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB4 B 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn10:
		GIB4 CCCCCCC 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB4 C 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn11:
		GIB5 AAAAAAA 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB5 A 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn12:
		GIB5 BBBBBBB 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB5 B 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	Spawn13:
		GIB5 CCCCCCC 4 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		GIB5 C 700 A_CustomMissile("BloodFlySM", 0, 0, random(0, 360), 2, random(-100, 100))
		Stop

	}
}

actor BloodFlySM
{
	Radius 0
	Height 0
	Scale 0.5
	Mass 100
	Speed 8
	Projectile
	-NOGRAVITY
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		BLUD C 1 A_CustomMissile ("BloodTrailSM", 0, 0, 0)
		Loop

	Death:
		BLUD C 1
		BLUD CBA 8
		Stop
	}
}

actor BloodTrailSM
{
	Radius 1
	Height 1
	Scale 0.5
	Projectile
	+NOGRAVITY
	+NOCLIP
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		BLUD CBA 3
		Stop
	}
}

ACTOR BloodFly
{
	Radius 0
	Height 0
	Scale 1.0
	Mass 50
	Speed 6
	Projectile
	-NOGRAVITY
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		BLUD C 1
		BLUD C 1 A_CustomMissile ("BloodTrail", 0, 0, 0)
		Loop

	Death:
		BLUD C 1
		BLUD CBA 8
		Stop
	}
}

ACTOR BloodFly2
{
	Radius 0
	Height 0
	Scale 0.6
	Mass 50
	Speed 8
	Projectile
	-NOGRAVITY
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		BLUD C 1
		Loop

	Death:
		BLUD C 1
		BLUD CBA 4
		Stop
	}
}

actor BloodTrail
{
	Radius 1
	Height 1
	Scale 1.0
	Projectile
	+NOGRAVITY
	+NOCLIP
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		BLUD CBA 3
		Stop
	}
}

actor Dryad replaces Stealthdoomimp
{
	Radius 28
	Height 36
	Scale 1
	Species "Plant"
	Mass 1000
	Painchance 220
	Monster
	Health 400
	+LOOKALLAROUND
	+NOINFIGHTING	
	+STANDSTILL	
	+SEEFRIENDLYMONSTERS
	BloodColor "42 97 51"
	Deathsound "Dryad/death"
	Painsound "seed/hit"
	States
	{
	Spawn:
		DRYD A 1 A_LookEx(0,0,1024,1024)
		Loop

	See:
		DRYD A 1 A_Chase
		loop
		
	Missile:
	TNT1 A 0 A_Jump(64,"Altmissile")
	DRYD B 4 A_Facetarget
	TNT1 A 0 A_SpawnProjectile("Dryadseed",56)
	DRYD CD 4
	DRYD B 4
	goto see
	
	AltMissile:
	DRYD B 4 A_Facetarget
	TNT1 A 0 A_SpawnProjectile("Dryadvinespawner",56)
	DRYD CD 4
	DRYD B 4
	goto see	
		
	Pain:
	DRYD C 8 A_Pain
	goto See

	Death:
		TNT1 A 0 A_Playsound("Dryad/death")
		DRYD E 1 A_XScream
		DRYD FGHIJK 4 
		DRYD K -1 A_Noblocking
		Stop
	}
}

actor Dryadseed
{
	Alpha 1
	Radius 8
	Height 8
	MissileHeight 8
	Speed 25
	Scale 0.5
	Damage (5)
	Projectile
	Seesound "seed/pop"
	DamageType "Normal"
	+BLOODSPLATTER

	States
	{
	Spawn:
		SEED ABCDEGH 1
		Loop

	XDeath:
		TNT1 A 0 A_PlaySound("seed/hit")
		Stop

	Death:
		SEDD ABC 6
		Stop
	}
}

actor Dryadvinespawner : Dryadseed
{
	Speed 30
	-Nogravity
	Gravity 0.2

	States
	{
	Spawn:
		TNT1 A 0 A_Playsound("vine/idle")
		SDAL ABCDEGH 2
		Loop

	Death:
		TNT1 A 0 A_JumpIf (z <= floorz, "SpawnVine")
		SDAD ABC 6
		Stop
		
	SpawnVine:
		TNT1 A 0 A_SpawnProjectile("DryadVine",0)
		SDAD ABC 6
		stop
		
	}
}

actor DryadVine
{
	Radius 24
	Height 36
	Scale 1
	Mass 1000
	Monster
	Health 60
	+LOOKALLAROUND
	+NOINFIGHTING
	+STANDSTILL
	BloodColor "42 97 51"
	States
	{
	Spawn:
	VINR ABCD 4
	goto See
	
	See:
	VIDL ABCDEFGHIJKLM 4 A_CustomMeleeattack(1)
	loop
	
	Death:
	VDTH A 4 A_XScream
	VDTH BCDE 4
	TNT1 A 0 A_Noblocking
	VDTH E -1
	stop
	}
}