actor Grarg replaces Demon
{
	Health 180
	Radius 16
	Height 56
	Speed 12
	PainChance 240
	Mass 90
	Scale 0.7
	DamageFactor "Explosive", 1.5
	Bloodcolor "e8 00 21"
	Monster
	Dropitem "Ammo357",96
	SeeSound "Grarg/Sight"
	DeathSound "Grarg/Death"
	ActiveSound "Grarg/Barks"
	PainSound "Grarg/Pain"
	Obituary "Amerigo was ripped to pieces by a Grarg."
	+FLOORCLIP
	+SEEFRIENDLYMONSTERS

	States
	{
	Spawn:
		TNT1 A 0 A_Jump(20, "Grumble")
		GRRG A 4 A_Look()
		Loop

	See:
		GRRG BBCCDDEE 2 A_Chase
		Loop

	Melee:
		TNT1 A 0 A_PlaySound("Grarg/Bite")
		GRRG FG 6 A_FaceTarget
		GRRG H 6 A_CustomMeleeAttack(random(2, 4) * 3, "imp/melee", "Melee", true)
		Goto See

	Missile:
		TNT1 A 0 A_PlaySound("Grarg/Bite")
		GRRG A 8 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0, 40, 0, 0)
		TNT1 A 0 ThrustThing(angle * 256 / 360, 15, 0, 0)
	Jumpup:
		GRRG F 6
		GRRG G 2 A_CustomMeleeAttack(random(2, 4) * 3, "imp/melee", "Melee", true)
	JumpDown:
		GRRG G 2 A_CheckFloor("Land")
		GRRG G 2
		GRRG G 3 A_CustomMeleeAttack(random(2, 4) * 3, "imp/melee", "Melee", true)
		Loop

	Land:
		GRRG H 4
		GRRG A 8 A_Stop
		Goto See

	Grumble:
		TNT1 A 0 A_PlaySound("Grarg/Idle",CHAN_AUTO,0.25)
		Goto Spawn

	Pain:
		GRRG I 8 A_Pain
		Goto See

	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_PlaySound("Grarg/Death")
		GRGD AB 4
		GRGD CD 8
		GRGD EFGH 4
		TNT1 A 0 A_NoBlocking
		GRGD I -1
		stop
	Xdeath:
		GRGD A 2 A_XSCREAM
		TNT1 A 0 A_Tossgib
		TNT1 A 0 A_NoBlocking
		Stop
	}
}

actor Lesco replaces ZombieMan
{
	Health 28
	Radius 16
	Height 48
	Speed 10
	PainChance 255
	Mass 65
	Scale 0.7
	Monster
	Dropitem "Ammo45",96
	Bloodcolor "e8 00 21"
	SeeSound "GLG/Attack"
	DeathSound "GLG/Death"
	ActiveSound "GLG/Idle"
	PainSound "GLG/Pain"
	Obituary "Amerigo was slashed to ribbons by a Lesco."
	+FLOORCLIP
	+SEEFRIENDLYMONSTERS
	+JUMPDOWN

	States
	{
	Spawn:
		TNT1 A 0 A_Jump(20, "Grumble")
		GGLG A 4 A_Look()
		Loop

	JumpDown:
		GGLG C 1 A_CheckFloor("Land")
		GGLG C 1
		GGLG C 1 A_CustomMeleeAttack(random(3, 5) * 5, "imp/melee", "Melee", true)
		Loop

	Land:
		GGLG FA 2 A_Stop
		Goto See

	See:
		GGLG AABBCCDD 2 A_Chase
		Loop

	Missile:
		GGLG EF 6 A_FaceTarget
		TNT1 A 0 A_SpawnProjectile("RockProjectile", 56)
		TNT1 A 0 A_PlaySound("GLG/Attack")
		GGLG G 6
		Goto See

	Melee:
		TNT1 A 0 A_PlaySound("GLG/Attack")
		GGLG EF 6 A_FaceTarget
		GGLG G 6 A_CustomMeleeAttack(random(2, 5) * 3, "imp/melee", "Melee", true)
		Goto See

	Grumble:
		TNT1 A 0 A_PlaySound("GLG/Idle",CHAN_AUTO,0.25)
		Goto Spawn

	Pain:
		GGLG H 6 A_Pain
		Goto See

	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_PlaySound("GLG/Death")
		GGLD A 7
		GGLD BCD 4
		TNT1 A 0 A_NoBlocking
		GGLD E -1
		Stop

	}
}

actor RockProjectile
{
	Alpha 1
	Radius 8
	Height 8
	MissileHeight 8
	Speed 30
	Gravity 0.3
	Scale 0.7
	Damage (10)
	Projectile
	Seesound "rock/bounce"
	DamageType "Normal"
	+BLOODSPLATTER
	+DOOMBOUNCE
	-NOGRAVITY

	States
	{
	Spawn:
		PROC ABCDEGHABCDEGH 3 A_AttachLightDef('Lighter','Lighter')
		TNT1 A 0 A_SetGravity(0.5)
		Loop

	XDeath:
		TNT1 A 0 A_PlaySound("rock/ow")
		Stop

	Death:
		PROC A 12 A_RemoveLight('Lighter')
		Stop
	}
}

actor RockProjectile2
{
Radius 10
Height 10
Missileheight 10
Speed 35
Gravity 0.2
Scale 1
Damage(15)
}

actor BigLesco replaces Doomimp
{
	Health 120
	Radius 24
	Height 56
	Speed 22
	PainChance 255
	Mass 65
	Scale 0.6
	Monster
	Dropitem "Ammo45",96
	Bloodcolor "e8 00 21"
	SeeSound "GLG/Attack"
	DeathSound "GLG/Death"
	ActiveSound "GLG/Idle"
	PainSound "GLG/Pain"
	Obituary "Amerigo was slashed to ribbons by a Lesco."
	+FLOORCLIP
	+SEEFRIENDLYMONSTERS
	+JUMPDOWN

	States
	{
	Spawn:
		TNT1 A 0 A_Jump(20, "Grumble")
		BGLC A 4 A_Look()
		Loop

	JumpDown:
		BGLC C 1 A_CheckFloor("Land")
		BGLC C 1
		BGLC C 1 A_CustomMeleeAttack(random(3, 5) * 5, "imp/melee", "Melee", true)
		Loop

	Land:
		BGLC FA 2 A_Stop
		Goto See

	See:
		BGLC ABCDEFG 3 A_Chase
		Loop

	Missile:
		BGLC I 6 A_FaceTarget
		TNT1 A 0 A_SpawnProjectile("RockProjectile2", 56)
		TNT1 A 0 A_PlaySound("GLG/Attack")
		BGLC JKLM 3 
		Goto See

	Melee:
		TNT1 A 0 A_PlaySound("GLG/Attack")
		BGLC I 6 A_FaceTarget
		BGLC JKLM 6 A_CustomMeleeAttack(random(2, 5) * 3, "imp/melee", "Melee", true)
		Goto See

	Grumble:
		TNT1 A 0 A_PlaySound("GLG/Idle",CHAN_AUTO,0.25)
		Goto Spawn

	Pain:
		BGLC H 2 A_Pain
		Goto See

	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_PlaySound("GLG/Death")
		BGLD A 7
		BGLD BCDE 4
		TNT1 A 0 A_NoBlocking
		BGLD F -1
		Stop

	}
}

actor Wyvern replaces Cacodemon
{

	Health 350
	Mass 120
	Speed 14
	Height 56
	Radius 32
	Painchance 220
	Bloodcolor "e8 00 21"
	Monster
	Obituary "Amerigo met a Wyvern."
	Seesound "Wyvern/pain"
	Deathsound "Wyvern/death"
	painsound "Wyvern/pain"
	+FLOORCLIP
	+FLOAT
	+NOGRAVITY
	States
	{
	Spawn:
		WYVN ABC 5 A_Look
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 5 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIf (health <= 100, "Rage")
		TNT1 A 0 A_Setspeed(14)
		WYVN ABC 3 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 3 A_Chase
		Loop

	Rage:
		TNT1 A 0 A_Setspeed(20)
		WYVN ABC 2 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 2 A_Chase
		WYVN ABC 2 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 2 A_Chase
		WYVN ABC 2 A_Chase
		TNT1 A 0 A_Playsound("Wyvern/flap",CHAN_AUTO)
		WYVN DCB 2 A_Chase
		goto see

	Missile:
		WYVN F 5 A_FaceTarget
		WYVN E 5 A_FaceTarget
		WYVN A 5 BRIGHT A_HeadAttack
		Goto See
	Pain:
		WYVN F 3
		WYVN F 3 A_Pain
		WYVN F 6
		Goto See
	Death:
		WYVD A 8 A_Playsound("Wyvern/scream",CHAN_AUTO)
		WYVD B 8 A_Scream
		WYVD C 8
		WYVD D 8
		WYVD D 8
		WYVD D 4 A_JumpIf (z <= floorz, "Crash")
		Wait
	Crash:
		TNT1 A 0 A_Playsound("Wyvern/fall",CHAN_AUTO)
		WYVD DE 5 A_NoBlocking
		WYVD F -1
		Stop



	Raise:
		WYVD F 8 A_UnSetFloorClip
		WYVD EDCBA 8
		Goto See
	}

}

actor Dryad replaces Stealthdoomimp
{
	Monster
	Radius 28
	Height 36
	Scale 1
	Mass 180
	Painchance 220
	Health 400
	GibHealth -100
	BloodColor "42 97 51"
	DeathSound "Dryad/death"
	PainSound "seed/hit"
	Species "Plant"
	+LOOKALLAROUND
	+NOINFIGHTING
	+STANDSTILL
	+SEEFRIENDLYMONSTERS
	+DONTTHRUST

	States
	{
	Spawn:
		DRYD A 1 A_LookEx(0,0,1024,1024)
		Loop

	See:
		DRYD A 1 A_Chase
		loop

	Missile:
	TNT1 A 0 A_Jump(64,"Altmissile")
	DRYD B 4 A_Facetarget
	TNT1 A 0 A_SpawnProjectile("Dryadseed",56)
	DRYD CD 4
	DRYD B 4
	goto see

	AltMissile:
	DRYD B 4 A_Facetarget
	TNT1 A 0 A_SpawnProjectile("Dryadvinespawner",56)
	DRYD CD 4
	DRYD B 4
	goto see

	Pain:
	DRYD C 8 A_Pain
	goto See

	Death:
		TNT1 A 0 A_Playsound("Dryad/death",CHAN_AUTO)
		DRYD E 1 A_XScream
		DRYD FGHIJK 4
		DRYD K -1 A_Noblocking
		Stop
	}
}

actor Dryadseed
{
	Alpha 1
	Radius 6
	Height 6
	Speed 25
	Scale 0.5
	Damage (5)
	Projectile
	Seesound "seed/pop"
	DamageType "Normal"
	+DONTTHRUST
	+BLOODSPLATTER

	States
	{
	Spawn:
		SEED ABCDEGH 1
		Loop

	XDeath:
		TNT1 A 0 A_PlaySound("seed/hit")
		Stop

	Death:
		SEDD ABC 6 A_PlaySound("seed/hit")
		Stop
	}
}

actor Huraseed : Dryadseed
{
	Scale 0.5
	Speed 32
	Gravity 1.4
	Damage (6)
	Radius 2
	Height 2
	-NOGRAVITY

	States
	{
	Spawn:
		SDAL ABCDEGH 1
		Loop

	Death:
		TNT1 A 0 A_PlaySound("seed/hit")
		SDAD ABC 6 Bright
		Stop
	}	
}

actor Dryadvinespawner : Dryadseed
{
	Speed 30
	-Nogravity
	Gravity 0.2

	States
	{
	Spawn:
		TNT1 A 0 A_Playsound("vine/idle")
		SDAL ABCDEGH 2
		Loop

	Death:
		TNT1 A 0 A_JumpIf (z <= floorz, "SpawnVine")
		TNT1 A 0 A_PlaySound("seed/hit")
		SDAD ABC 6 
		Stop

	SpawnVine:
		TNT1 A 0 A_SpawnProjectile("DryadVine",0)
		TNT1 A 0 A_PlaySound("seed/hit")
		SDAD ABC 6
		stop

	}
}

actor DryadVine
{
	Monster
	Health 60
	Mass 40
	Radius 24
	Height 36
	Scale 1
	BloodColor "42 97 51"
	+LOOKALLAROUND
	+NOINFIGHTING
	+STANDSTILL
	+DONTTHRUST

	States
	{
	Spawn:
		VINR ABCD 4
		Goto See

	See:
		VIDL ABCDEFGHIJKLM 4 A_CustomMeleeAttack(1)
		Loop

	Death:
		VDTH A 4 A_XScream
		VDTH BCDE 4
		TNT1 A 0 A_NoBlocking
		VDTH E -1
		Stop
	}
}